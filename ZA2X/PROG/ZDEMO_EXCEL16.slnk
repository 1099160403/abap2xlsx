<?xml version="1.0" encoding="utf-16"?>
<PROG NAME="ZDEMO_EXCEL16" VARCL="X" SUBC="1" CNAM="VONDRUSKA" CDAT="20100914" UNAM="FEMIA" UDAT="20110202" VERN="000072" RSTAT="T" RMAND="001" RLOAD="E" FIXPT="X" SDATE="20110202" STIME="191051" IDATE="20110202" ITIME="191051" UCCHECK="X">
 <textPool>
  <language SPRAS="E">
   <textElement ID="R" ENTRY="abap2xlsx Demo: Drawings" LENGTH="24 "/>
   <textElement ID="S" KEY="P_PATH" ENTRY="D       ." LENGTH="9 "/>
  </language>
 </textPool>
 <source>*&amp;---------------------------------------------------------------------*
*&amp; Report  ZDEMO_EXCEL16
*&amp;
*&amp;---------------------------------------------------------------------*
*&amp;
*&amp;
*&amp;---------------------------------------------------------------------*

REPORT  zdemo_excel16.

DATA: lo_excel                TYPE REF TO zcl_excel,
      lo_excel_writer         TYPE REF TO zif_excel_writer,
      lo_worksheet            TYPE REF TO zcl_excel_worksheet,
      lo_drawing              TYPE REF TO zcl_excel_drawing.

DATA: lv_file                 TYPE xstring,
      lv_bytecount            TYPE i,
      lt_file_tab             TYPE solix_tab.

DATA: lv_full_path      TYPE string,
      lv_workdir        TYPE string,
      lv_file_separator TYPE c.

DATA: ls_io TYPE skwf_io.

CONSTANTS: lv_default_file_name TYPE string VALUE &apos;16_Drawings.xlsx&apos;.

PARAMETERS: p_path    TYPE zexcel_export_dir,
            p_objid   TYPE sdok_docid DEFAULT &apos;456694429165174BE10000000A1550C0&apos;, &quot; Question mark in standard Web Dynpro WDT_QUIZ
            p_class   TYPE sdok_class DEFAULT &apos;M_IMAGE_P&apos;,
            pobjtype  TYPE skwf_ioty  DEFAULT &apos;P&apos;.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_path.
  lv_workdir = p_path.
  cl_gui_frontend_services=&gt;directory_browse( EXPORTING initial_folder  = lv_workdir
                                              CHANGING  selected_folder = lv_workdir ).
  p_path = lv_workdir.

INITIALIZATION.
  cl_gui_frontend_services=&gt;get_sapgui_workdir( CHANGING sapworkdir = lv_workdir ).
  cl_gui_cfw=&gt;flush( ).
  p_path = lv_workdir.

START-OF-SELECTION.

  IF p_path IS INITIAL.
    p_path = lv_workdir.
  ENDIF.
  cl_gui_frontend_services=&gt;get_file_separator( CHANGING file_separator = lv_file_separator ).
  CONCATENATE p_path lv_file_separator lv_default_file_name INTO lv_full_path.

  &quot; Creates active sheet
  CREATE OBJECT lo_excel.

  &quot;Load samle image
  DATA: lt_bin TYPE solix_tab,
        lv_len TYPE i,
        lv_content TYPE xstring,
        ls_key TYPE wwwdatatab.

  CALL METHOD cl_gui_frontend_services=&gt;gui_upload
    EXPORTING
      filename                = &apos;c:\Program Files\SAP\FrontEnd\SAPgui\wwi\graphics\W_bio.bmp&apos;
      filetype                = &apos;BIN&apos;
    IMPORTING
      filelength              = lv_len
    CHANGING
      data_tab                = lt_bin
    EXCEPTIONS
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      invalid_type            = 5
      no_authority            = 6
      unknown_error           = 7
      bad_data_format         = 8
      header_not_allowed      = 9
      separator_not_allowed   = 10
      header_too_long         = 11
      unknown_dp_error        = 12
      access_denied           = 13
      dp_out_of_memory        = 14
      disk_full               = 15
      dp_timeout              = 16
      not_supported_by_gui    = 17
      error_no_gui            = 18
      OTHERS                  = 19.
  IF sy-subrc &lt;&gt; 0.
*    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
*               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  CALL FUNCTION &apos;SCMS_BINARY_TO_XSTRING&apos;
    EXPORTING
      input_length = lv_len
    IMPORTING
      buffer       = lv_content
    TABLES
      binary_tab   = lt_bin
    EXCEPTIONS
      failed       = 1
      OTHERS       = 2.
  IF sy-subrc &lt;&gt; 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  &quot; Get active sheet
  lo_worksheet = lo_excel-&gt;get_active_worksheet( ).
  lo_worksheet-&gt;set_title( &apos;Sheet1&apos; ).
  lo_worksheet-&gt;set_cell( ip_column = &apos;B&apos; ip_row = 2 ip_value = &apos;Image from web repository (SMW0)&apos; ).

  &quot; create global drawing, set position and media from web repository
  lo_drawing = lo_excel-&gt;add_new_drawing( ).
  lo_drawing-&gt;set_position( ip_from_row = 3
                            ip_from_col = &apos;B&apos; ).

  ls_key-relid = &apos;MI&apos;.
  ls_key-objid = &apos;SAPLOGO.GIF&apos;.
  lo_drawing-&gt;set_media_www( ip_key = ls_key
                             ip_width = 166
                             ip_height = 75 ).

  &quot; assign drawing to the worksheet
  lo_worksheet-&gt;add_drawing( lo_drawing ).

  &quot; another drawing from a XSTRING read from a file
  lo_worksheet-&gt;set_cell( ip_column = &apos;B&apos; ip_row = 8 ip_value = &apos;Image from a file (c:\Program Files\SAP\FrontEnd\SAPgui\wwi\graphics\W_bio.bmp)&apos; ).
  lo_drawing = lo_excel-&gt;add_new_drawing( ).
  lo_drawing-&gt;set_position( ip_from_row = 9
                            ip_from_col = &apos;B&apos; ).
  lo_drawing-&gt;set_media( ip_media       = lv_content
                         ip_media_type = &apos;bmp&apos;
                         ip_width = 83
                         ip_height = 160 ).

  lo_worksheet-&gt;add_drawing( lo_drawing ).

  ls_io-objid   = p_objid.
  ls_io-class   = p_class.
  ls_io-objtype = pobjtype.
  IF ls_io IS NOT INITIAL.
    &quot; another drawing from a XSTRING read from a file
    lo_worksheet-&gt;set_cell( ip_column = &apos;B&apos; ip_row = 18 ip_value = &apos;Mime repository (by default Question mark in standard Web Dynpro WDT_QUIZ)&apos; ).
    lo_drawing = lo_excel-&gt;add_new_drawing( ).
    lo_drawing-&gt;set_position( ip_from_row = 19
                              ip_from_col = &apos;B&apos; ).
    lo_drawing-&gt;set_media_mime( ip_io       = ls_io
                                ip_width = 126
                                ip_height = 145 ).

    lo_worksheet-&gt;add_drawing( lo_drawing ).
  ENDIF.


  CREATE OBJECT lo_excel_writer TYPE zcl_excel_writer_2007.
  lv_file = lo_excel_writer-&gt;write_file( lo_excel ).

  &quot; Convert to binary
  CALL FUNCTION &apos;SCMS_XSTRING_TO_BINARY&apos;
    EXPORTING
      buffer        = lv_file
    IMPORTING
      output_length = lv_bytecount
    TABLES
      binary_tab    = lt_file_tab.
*  &quot; This method is only available on AS ABAP &gt; 6.40
*  lt_file_tab = cl_bcs_convert=&gt;xstring_to_solix( iv_xstring  = lv_file ).
*  lv_bytecount = xstrlen( lv_file ).

  &quot; Save the file
  cl_gui_frontend_services=&gt;gui_download( EXPORTING bin_filesize = lv_bytecount
                                                    filename     = lv_full_path
                                                    filetype     = &apos;BIN&apos;
                                           CHANGING data_tab     = lt_file_tab ).</source>
</PROG>
